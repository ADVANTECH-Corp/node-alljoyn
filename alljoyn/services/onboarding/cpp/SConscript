# Copyright AllSeen Alliance. All rights reserved.
#
#    Permission to use, copy, modify, and/or distribute this software for any
#    purpose with or without fee is hereby granted, provided that the above
#    copyright notice and this permission notice appear in all copies.
#
#    THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
#    WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
#    MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
#    ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
#    WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
#    ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
#    OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.


Import('onb_env')

if not onb_env.has_key('_ALLJOYNCORE_'):
    onb_env.Append(LIBS = ['alljoyn'])
    if onb_env['BR'] == 'on' :
        brFile = onb_env.subst('$DISTDIR') + '/cpp/lib/BundledRouter.o'
        onb_env['brobj'] = File(brFile)
        onb_env['ajrlib'] = 'ajrouter'
    if onb_env['OS'] == 'openwrt':
        onb_env.AppendUnique(LIBS = [ 'stdc++', 'pthread' ])
    if onb_env['OS'] == 'linux':
        onb_env.AppendUnique(LIBS = [ 'rt', 'pthread' ])

# Make onboarding dist a sub-directory of the alljoyn dist.  This avoids any conflicts with alljoyn dist targets.
onb_env['ONBOARDING_DISTDIR'] = onb_env['DISTDIR'] + '/onboarding'

onb_env.Install('$ONBOARDING_DISTDIR/inc/alljoyn/onboarding', onb_env.Glob('inc/alljoyn/onboarding/*.h'))

# libraries
onb_env.Install('$ONBOARDING_DISTDIR/lib', onb_env.SConscript('src/SConscript', exports = ['onb_env']))

# Sample programs
if onb_env['BUILD_SERVICES_SAMPLES'] == 'on':
    onb_env.Install('$ONBOARDING_DISTDIR/bin', onb_env.SConscript('samples/SConscript', exports = ['onb_env']))

# onboarding-daemon
onb_env.Install('$ONBOARDING_DISTDIR/bin', onb_env.SConscript('src/OnboardingDaemon/SConscript', exports = ['onb_env']))

# Build docs
installDocs = onb_env.SConscript('docs/SConscript', exports = ['onb_env'])
onb_env.Depends(installDocs, onb_env.Glob('$ONBOARDING_DISTDIR/inc/alljoyn/onboarding/*.h'));
